明示してなければ Excel 2007 (Windows) で確認。

※ここでは数式についてのコメントを → で表現しています。実際には数式にこのようなかたちでコメントを記述することは出来ません。

= リテラルと型 =
数式の中では数値はそのまま記述することができます。

<pre>
=300 → 300
=3.14 → 3.14
</pre>

文字列はダブルクォーテーション " で囲みます。文字列中にダブルクォーテーション を含める場合は "" と記述します。

<pre>
="文字" → 文字
="ダブルクォーテーション は""強調""や""引用""を意味する" → ダブルクォーテーション は"強調"や"引用"を意味する
</pre>

数値と文字はある程度自動的に変換されます。

<pre>
=1+"2" → 3 ※文字列が数値に変換される例
=FIND(2, "ABC123") → 5 ※数値が文字列に変換される例
</pre>

ただし、後述の比較演算子やMATCHなどでは変換しないまま比較されるので、数値として扱うか文字として扱うかきっちり決めておいたほうが安全です。

<pre>
=IF(1="1", "一致", "不一致") → 不一致
</pre>

ほかに、真か偽かの二値だけをとる真偽値があり、TRUE (真)、またはFALSE (偽) で示します。真偽値を数値として扱う場合、TRUEは1、FALSEは0になります。文字列として扱う場合、それぞれTRUE、FALSEという文字列になります。

<pre>
=(1=1) → TRUE
=(3>5) → FALSE
=TRUE+1 → 2
="TRUE is "&TRUE → TRUE is TRUE
</pre>

数値か文字列で、日付や時間を示すこともできます。数値として扱う場合は、1日を1とし、日付や日付を含む時刻は1900年1月1日00:00からの経過時間、日付を含まない時刻・時間は00:00からの経過時間で示されます。

<pre>
=("2010/9/8"+1) → 2010/9/9 ※セルの書式を「短い日付形式」にした場合
</pre>

= 関数、引数 =
関数は「関数名 (引数1, 引数2, …)」の形式で使用することができます。引数は省略可能なものもあります (カッコは省略できません。引数のない関数はPI()のように使います)。関数を実行すると何らかの値を返すので、そのままセルの値とすることもできますし、他の関数の引数にしたり、演算子で演算することもできます。

= エラー =
数式の結果がエラーになる場合があります。たとえば0で除算した場合は <tt>#DIV/0!</tt> というエラーになります。

<pre>
=1/0 → #DIV/0!
</pre>

エラーは数値でも文字列でもないので、関数や演算子による演算はほとんど行うことができません。エラーであるかどうか判定したい場合は <tt>ISERROR</tt> 関数を使用してください。

<pre>
=ISERROR(1/0) → TRUE
</pre>

エラーの意味と対処方法については下記ページを参照してください。

* [http://office.microsoft.com/ja-jp/excel-help/HP010066255.aspx#BMcorrect_an_error_value 数式エラーを確認して修正する - Excel - Microsoft Office]

= 算術演算子、比較演算子、論理演算 =
== + : 加算 ==
<pre>=5+3 → 8</pre>

== - : 減算 ==
<pre>=5-2 → 3</pre>

== * : 乗算 ==
<pre>=5*3 → 15</pre>

== / : 除算 ==
整数同士の除算でも、整数が返るとは限りません。
<pre>
=5/3 → 1.666667 ※セルの書式により表示される桁数は異なります
=ROUNDDOWN(5/3,0) → 1 ※小数点以下切り捨て
</pre>

== MOD(値, 除数) : 剰余 ==
<pre>=MOD(5,3) → 2</pre>

== ^ : 累乗 ==
<pre>=5^3 → 125</pre>

== & : 文字列の結合 ==
<pre>
="ABC"&"DEF" → ABCDEF
=100&200 → 100200
</pre>

== = : 両辺が等しければTRUE、それ以外はFALSE ==
等号2つ == じゃなくて、1つ = なので注意。

<pre>
=(1=1) → TRUE
=(1=2) → FALSE
=("string"="string") → TRUE
=("string"="String") → TRUE ※大文字小文字は区別されない
=("string"="ｓｔｒｉｎｇ") → FALSE ※半角全角は区別される
=(1="1") → FALSE
</pre>

== <> : 両辺が等しくなければTRUE、それ以外はFALSE ==
!= は使えません。

<pre>
=(1<>2) → TRUE
=(1<>1) → FALSE
=("string"<>"streich") → TRUE
</pre>

== > : 左辺が右辺より大きければTRUE、それ以外はFALSE ==
<pre>
=(2>1) → TRUE
=(1>1) → FALSE
</pre>

== < : 左辺が右辺より小さければTRUE、それ以外はFALSE ==
<pre>
=(1<2) → TRUE
=(1<1) → FALSE
</pre>

== >= : 左辺が右辺以上ならTRUE、それ以外はFALSE ==
<pre>
=(2>=1) → TRUE
=(1>=1) → TRUE
=(0>=1) → FALSE
</pre>

== <= : 左辺が右辺以下ならTRUE、それ以外はFALSE ==
<pre>
=(1<=2) → TRUE
=(1<=1) → TRUE
=(1<=0) → FALSE
</pre>

== AND(真偽式1, 真偽式2, …) : 論理積 ==
<pre>
=AND(TRUE,TRUE) → TRUE
=AND(TRUE,FALSE) → FALSE
=AND(FALSE,FALSE) → FALSE
=AND(3<5,5<7) → TRUE
</pre>

== OR(真偽式1, 真偽式2, …) : 論理和 ==
<pre>
=OR(TRUE,TRUE) → TRUE
=OR(TRUE,FALSE) → TRUE
=OR(FALSE,FALSE) → FALSE
=OR(3<0,3<5) → TRUE
</pre>

== NOT(真偽式) : 否定 ==
<pre>
=NOT(TRUE) → FALSE
=NOT(FALSE) → TRUE
=NOT(3<5) → FALSE
</pre>

= セル、セル範囲の参照 =
{|class="wikitable" style="float:left"
!style="width:3em;"|
!style="width:3em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
!style="width:3em;"| E
!style="width:3em;"| F
!style="width:3em;"| G
|-
! 1
|style="background:#fada7a"| 
| 
|style="background:#d2da7a"| 
|style="background:#d2da7a"| 
| 
|style="background:#00b0f0"| 
| 
|-
! 2
|
|
|style="background:#d2da7a"|
|style="background:#d2da7a"|
|
|style="background:#00b0f0"|
|
|-
! 3
|
|
|
|
|
|style="background:#00b0f0"|
|
|-
! 4
|style="background:#9fb8cd"|
|style="background:#9fb8cd"|
|style="background:#9fb8cd"|
|
|
|style="background:#00b0f0"|
|
|-
! 5
|
|style="background:#9fb8cd"|
|style="background:#9fb8cd"|
|
|
|style="background:#00b0f0"|
|
|-
! 6
|
|
|style="background:#9fb8cd"|
|
|
|style="background:#00b0f0"|
|
|-
! 7
|
|
|
|
|
|style="background:#00b0f0"|
|
|-
! 8
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#7030a0"|
|style="background:#c00000"|
|-
! 9
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#c00000"|
|style="background:#7030a0"|
|style="background:#c00000"|
|-
! 10
|
|
|
|
|
|style="background:#00b0f0"|
|
|}

セルは列名と行番号で指定します。A列1行のセルなら<span style="background:#fada7a"> A1 </span>です。

四角形のセル範囲は左上端と右下端のセルをコロンでつないで指定します。たとえば<span style="background:#d2da7a"> C1:D2 </span>で4つのセルを含むセル範囲を示します。<span style="background:#00b0f0"> F:F </span>、<span style="background:#c00000"> 8:9 </span>のように、行番号/列名を省略すると、指定した列、行の全体を示します。

四角形でないセル範囲は、セルか、四角形のセル範囲をコンマでつないで指定します。<span style="background:#9fb8cd"> A4,B4:C5,C6 </span>で6つのセルを含むセル範囲を示します。

別シートにあるセルを参照するには、セル指定、四角形のセル範囲指定の前に「シート名!」を付与します。そのシートが別ブックにあるなら「[ブック名] シート名!」にします。

ただし、通常は数式の入力中にセルをクリックして参照するのがもっとも簡単です。四角形のセル範囲はドラッグで、コンマ区切りはCtrlを押しながらクリック/ドラッグで実現できます。

<br style="clear:both" />

= 絶対参照と相対参照 =
{|class="wikitable" style="float:left"
!style="width:3em;"| 
!style="width:3em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
!style="width:3em;"| E
!style="width:3em;"| F
!style="width:3em;"| G
|-
! 1
|
|
|
|
|
|
|
|-
! 2
|
|style="background:#fada7a"| =X1
|style="background:#fada7a"| =Y1
|
|style="background:#d2da7a"| =$X1
|style="background:#d2da7a"| =$X1
|
|-
! 3
|
|style="background:#fada7a"| =X2
|style="background:#fada7a"| =Y2
|
|style="background:#d2da7a"| =$X2
|style="background:#d2da7a"| =$X2
|
|-
! 4
|
|
|
|
|
|
|
|-
! 5
|
|style="background:#9fb8cd"| =X$1
|style="background:#9fb8cd"| =Y$1
|
|style="background:#b88472"| =$X$1
|style="background:#b88472"| =$X$1
|
|-
! 6
|
|style="background:#9fb8cd"| =X$1
|style="background:#9fb8cd"| =Y$1
|
|style="background:#b88472"| =$X$1
|style="background:#b88472"| =$X$1
|
|-
! 7
|
|
|
|
|
|
|
|}

セル(セル範囲含む)の指定には絶対参照と相対参照の2種類があります。これはセル指定の列名、行番号の前に$があるかないかで指定します。$があれば絶対参照、なければ相対参照です。

この違いは、このセル参照を含むセルを別のセルにコピーしたときに現れます。

絶対参照の場合は、別のセルにコピーした際にも参照するセルは変化しません。相対参照の場合は、コピー元とコピー先のセルの位置関係に応じて参照するセルが変化します。

付表の例ではB2、E2、B5、E5にそれぞれさまざまな方法でX1への参照を書き、それぞれを横と下にコピーしたものです。$のついた部分は参照先が変化せず、ついていない部分はセルの位置に応じて参照先が変化しています。

絶対参照と相対参照の切り替えは、数式の編集時セル指定の部分にカーソルがある状態でF4キーを押すのが便利です。F4キーを押すたび、A10 → $A$10→ A$10 → $A10 → A10 ... のように変化します。

<br style="clear:both" />

= よく使う関数 =
== LEFT(対象文字列, 文字数) ==
<対象文字列>の左から<文字数>文字を返す。

<pre>
=LEFT("ABCDEF", 3) → ABC
</pre>

== MID(対象文字列, 開始位置, 文字数) ==
<対象文字列>の<開始位置>文字目から<文字数>文字を返す。<文字数>が充分大きいと<開始位置>以降すべての文字を返す。

<pre>
=MID("ABCDEF", 3, 2) → "CD"
=MID("ABCDEF", FIND("C","ABCDEF"), 100) → "CDEF" ※Cがある位置から最後まで
</pre>

== FIND(検索文字列, 対象文字列, [開始位置]) ==
<対象文字列>の<開始位置>文字目から<検索文字列>を探し、見つかったら<検索文字列>の1文字目がある位置を返す。<開始位置>省略時は1文字目から。見つからなければ#VALUEエラーを返す。

<pre>
=FIND("D","ABCDEFABCDEF") → 4
=FIND("D","ABCDEFABCDEF",5) → 10
=FIND("1","ABCDEFABCDEF") → #VALUE
</pre>

== MATCH(検索値, 検索範囲, 検索方法) ==
{|class="wikitable" style="float:left"
!style="width:3em;"| 
!style="width:3em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
|-
! 1
| い || ろ || は || に
|-
! 2
| ほ || へ || と || ち
|-
! 3
| り || ぬ || る || を
|-
! 4
| わ || か || よ || た
|}

<検索範囲>を<検索方法>にもとづいて<検索値>で検索し、最初にヒットしたセルの<検索範囲>内での行位置/列位置を返す。<検索範囲>は1列n行かn列1行のセル範囲。検索方法はややこしいのでつねに0を指定することを強く推奨する。0を指定すると完全一致した場合のみヒットしたとみなし、ヒットしなければ#N/Aエラーを返す。

<br style="clear:both" />

<pre>
(付表において)
=MATCH("ぬ", B:B, 0) → 3 ※B列の3行目でヒット
=MATCH("ぬ", 3:3, 0) → 2 ※3行目の2列目でヒット
=MATCH("ぬ", B3:B4, 0) → 1 ※この範囲内の1行目でヒット
=MATCH("ぬ", A:A, 0) → #N/A ※A列ではヒットしない
</pre>

== INDEX(範囲, 行位置, 列位置) ==
{|class="wikitable" style="float:left"
!style="width:3em;"| 
!style="width:3em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
|-
! 1
| い || ろ || は || に
|-
! 2
| ほ || へ || と || ち
|-
! 3
| り || ぬ || る || を
|}

<範囲>内の<行位置>、<列位置>にあるセルの値を返す。<範囲>はn列n行のセル範囲だが、1列n行かn列1行のセル範囲の場合は、3つ目の引数を省略可能(2つ目の引数は、範囲が1列n行なら行位置、範囲がn列1行なら列位置と見なされる)。

<br style="clear:both" />

<pre>
(付表において)
=INDEX(A1:D3,3,2) → ぬ ※A1:D3の3行目2列目 
=INDEX(B2:C3,2,1) → ぬ ※B2:C3の2行目1列目 
=INDEX(B:B,3) → ぬ ※B列の3行目
=INDEX(3:3,2) → ぬ ※3行目の2列目
=INDEX(A:A,MATCH("ぬ",B:B,0)) → り ※B列が「ぬ」である行(3行目)のA列のセルの値
</pre>

== IF(条件式, [真の場合の値], [偽の場合の値]) ==
<条件式>がTRUEなら<真の場合の値>、FALSEなら<偽の場合の値>を返す。<真の場合の値>、<偽の場合の値>のうち片方は省略しても良いが、省略時の挙動がややこしいので省略しないことを推奨する。

<pre>
=IF(1<3, "1は3より小さい", "1は3より大きい") → 1は3より小さい
</pre>

== COUNTIF(範囲, 条件) ==
{|class="wikitable" style="float:left"
!style="width:3em;"| 
!style="width:3em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
|-
! 1
| 10 || 20 || 30 || 40
|-
! 2
| い || ろ || い || ろ
|}

<範囲>内に<条件>に一致するセルがいくつあるかを返す。

<br style="clear:both" />

<pre>
=COUNTIF(A1:D2,20) → 1
=COUNTIF(A1:D2,"<=30") → 3
=COUNTIF(A1:D2,"い") → 2
</pre>

== SUM(範囲) ==
{|class="wikitable" style="float:left"
!style="width:3em;"| 
!style="width:3em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
|-
! 1
| 10 || 20 || 30 || 40
|-
! 2
| い || ろ || い || ろ
|}

<範囲>内の数値をすべて合計した値を返す。

<br style="clear:both" />

<pre>
=SUM(A1:D2) → 100
</pre>

== ISERROR(式) ==
<式>がエラーならTRUE、エラーでなければFALSEを返す。FIND、MATCH、INDEXなどが成功したかによって処理を変えたいときに便利。

<pre>
=ISERROR(FIND("A","123456")) → TRUE
=IF(ISERROR(FIND("A","123456")),"not found","found") → not found
</pre>
 
== GETPIVOTDATA(値の系列, 参照するピボットテーブル, 行/列名1, 行/列の値1, 行/列名2, 行/列の値2, …) ==
{|class="wikitable" style="float:left"
!style="width:3em;"| 
!style="width:10em;"| A
!style="width:3em;"| B
!style="width:3em;"| C
!style="width:3em;"| D
!style="width:3em;"| E
!style="width:3em;"| F
|-
! 3
| データの個数 / ID || 学年 ||  ||  ||  || 
|-
! 4
| 学部 || 1 || 2 || 3 || 4 || 総計
|-
! 5
| 医 || 1 || 1 ||  || 1 || 3
|-
! 6
| 教 || 1 || 2 ||  ||  || 3
|-
! 7
| 工 || 1 ||  || 1 || 1 || 3
|-
! 8
| 人 ||  || 1 ||  || 1 || 2
|-
! 9
| 生 ||  || 1 || 1 || 2 || 4
|-
! 10
| 総計 || 3 || 5 || 2 || 5 || 15
|}

<参照するピボットテーブル>から、<行/列名X>が<行/列の値X>の<値の系列>の値を返します。セル参照と同じように、数式中でピボットテーブル内のセルをクリックすればこの関数を使った参照が作られるので、これを利用して、必要があれば書換えるのが簡単。

<br style="clear:both" />

<pre>
=GETPIVOTDATA("ID",$A$3,"学年",2,"学部","教") → 2
※$A$3にあるピボットテーブルの「学年」が「2」で学部が「教」の「ID」の個数
</pre>